 DemoGrids connected;inputs;cc;e;fmts;ticker;fixed;value;price;quantity;comments;titles;g;values;widths;inputix;types;fmtix;ws;fcols;bcols;fcolix;bcolix;fontix;fonts
 JSWC.CONNECTED←connected
 :If ~connected
    JSWC.(CONNECTED USEWIN32 LOGMODES)←0 1 ⎕A  ⍝ /// While debugging
 :EndIf
 ⎕PW←1000

 n←10
 f←'×'
 allf←'+-×÷'

 'F1'eWC'Form' 'Grids'(50 50)(800 800)('Coord' 'Pixel')
 'F1.MB'eWC'MenuBar'
 'F1.MB.File'eWC'Menu' '&File'
 'F1.MB.Col'eWC'Menu' '&Colours'
 'F1.MB.Col.Red'eWC'MenuItem' '&Red'('Event' 'Select' 'JSWCUpdate')
 'F1.MB.Col.Blue'eWC'MenuItem' '&Blue'('Event' 'Select' 'JSWCUpdate')
 'F1.MB.Col.Green'eWC'MenuItem' '&Green'('Event' 'Select' 'JSWCUpdate')

 'F1.FIXED' eWC 'Font' 'Courier New' 18
 'F1.PROP' eWC 'Font' 'Segoe UI' 18

 ticker←'ABCD' 'DEFG' 'HIJL'
 fixed←0 1 0
 quantity←5123 6750 11231
 price←1.23 2.34 3.45
 value←quantity×price
 comments←'' 'Cannot buy or sell any of these'

 titles←'Ticker' 'Fixed' 'Price' 'Quantity' 'Value' 'Comments'
 widths←     40      30      50         60     100        200

 e←'Event' ('CellChanged' 'CellMove') 'JSWCUpdate'
 (g←'F1.Holdings') eWC'Grid'('ColTitles' titles)('Posn'(40 10))('Size'(100,(1+≢widths)++/widths))('TitleWidth' 0)e('CellWidths' widths)
 g eWS 'ShowInput' 0

 (g,'.OUTPUT')eWC'Label'
 (g,'.TEXT')eWC'Edit'('Event' 'KeyPress' 'JSWCUpdate')
 (g,'.INT') eWC'Edit'('FieldType' 'Numeric')('Decimals' 0)
 (g,'.FLOAT') eWC'Edit'('FieldType' 'Numeric')('Decimals' 2)
 (g,'.CHECK')eWC'Button'('Style' 'Check')
 (g,'.OUTPUTR')eWC'Label'('Justify' 'Right')

 inputs←g∘,¨'.OUTPUT' '.TEXT' '.INT' '.FLOAT' '.CHECK' '.OUTPUTR'
 values←⍉↑ticker  fixed               price    quantity   value  comments

 types←  'OUTPUT' 'CHECK'            'FLOAT'   'INT'     'OUTPUTR' 'TEXT'
 inputix←(⍴values)⍴inputs⍳(g,'.')∘,¨types
 inputix[⍸values[;2]=1;4]←6 ⍝ Quantity cannot be changed if "Fixed"

 fmts←    '4A1'   'O0<No >O1<Yes>I3' 'CF10.2'  'CI10'    'CI10'   '50A1'
 fmtix←(⍴values)⍴⍳≢fmts

 fcols←(0 0 0)(255 0 0)(0 255 0)
 bcols←(255 255 255)(127 127 255)

 fcolix←(⍴values)⍴1             ⍝ All black text
 bcolix←(⍴values)⍴(≢titles)/1 2 ⍝ Alternating white & blue

 fonts←'F1.PROP' 'F1.FIXED'
 fontix←(⍴values)⍴1+(≢titles)↑1

 ws←getTypeWS inputs inputix fmts fmtix fcols fcolix bcols bcolix fonts fontix
 g eWS(⊂'Values' values),ws
 g eWS 'CurCell' (1 3)

 'F1.Average'eWC'Label'('Total value: ',1⍕+/value)(145 400)(23 200)
 'F1.Home'eWC'Button' 'Home'(145 10)(23 100)('Event' 'Select' 'JSWCUpdate')
 'F1.Where'eWC'Button' 'CurCell'(145 120)(23 100)('Event' 'Select' 'JSWCUpdate')
 'F1.SelText'eWC'Button' 'SelText'(145 230)(23 100)('Event' 'Select' 'JSWCUpdate')

 'F1.TableSize'eWC'Edit' '10'(210 10)(23 100)('FieldType' 'Numeric')('Event' 'Change' 'JSWCUpdate')
 'F1.Function'eWC'Combo'(,¨allf)f(210 120)(23 100)('SelItems'(allf=f))('Event' 'Select' 'JSWCUpdate')
 'F1.CalcBtn'eWC'Button' 'Calc'(210 230)(23 100)('Event' 'Select' 'JSWCUpdate')
 'F1.Table'eWC'Grid'(∘.×⍨⍳n)(240 10)(550 780)

 'F1.PasswordField'eWC'Edit' 'Password'(210 340)(23 100)('Password' '*')('Event' 'Change' 'JSWCUpdate')

 cc←g eWG 'CurCell'
 :If cc≢1 3
     'F1.Average'eWS 'Caption' ('CurCell is ',(⍕cc),', should be 1 3')
 :EndIf

 :If ~connected
     ⎕EX 'F1.HR'
 :EndIf
