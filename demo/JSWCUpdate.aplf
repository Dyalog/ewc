 JSWCUpdate args;e;scores;cols;fcol;pass;ii;ids;target;row;col;scroll;sel;mouse;changed;newval
 Event←⎕NS''
 e←Event.Event←⎕NS''
 e.(ID EventName Info)←(2↑args),⊂2↓args
 ⎕←'JSWCUpdate: ',⎕JSON Event

 :Select 1⊃args
 :Case 'F1.Home'
     ∆NQ 'F1.Players' 'CellMove' 1 1
 :Case 'F1.PasswordField' ⍝ Edit
     pass←'F1.PasswordField'∆WG'Value'
     'F1.Average'∆WS'Caption'('Password is ',pass)
 :Case 'F1.Function' ⍝ Dropdown
     f←allf[1⍳⍨'F1.Function'∆WG'SelItems']
 :Case 'F1.TableSize' ⍝ Edit
     f←allf[1⍳⍨'F1.Function'∆WG'SelItems']
     n←⊃'F1.TableSize'∆WG'Value'
     :If 'KeyPress'≡2⊃args
         ⎕←args
     :EndIf
 :Case 'F1.Players'
     :Select 2⊃args
     :Case 'CellMove'
         (row col scroll sel mouse changed newval)←7↑2↓args
         'F1.Average'∆WS'Caption'('CellMove to Row: ',(⍕row),' Col: ',(⍕col))
         →0
     :EndSelect
     scores←('F1.Players'∆WG'Values')[;3]
     'F1.Average'∆WS'Caption'('Average score:',1⍕+/scores÷≢scores)
     →0
 :Case 'F1.Players.Name' ⍝ KeyPress
     :If 'KeyPress'≡2⊃args
         'F1.Average'∆WS'Caption'('KeyPress: ''',(3⊃args),''', code: ',(⍕4⊃args),', shift: ',(⍕6⊃args))
     :EndIf
 :Case 'F1.CalcBtn'
     f←allf[1⍳⍨'F1.Function'∆WG'SelItems']
     n←'F1.TableSize'∆WG'Value'
 :CaseList 'F1.Months' 'F1.LEFT.Months' ⍝ Treeview
     ii←(1⊃args)∆WG'ImageIndex'
     ii[3⊃args]←1+'Expanding'≡2⊃args ⍝ Set Image Index to open or closed
     (1⊃args)∆WS'ImageIndex'ii
 :CaseList cols←'F1.MB.Col.'∘,¨'Red' 'Blue' 'Green'
     fcol←(cols⍳args[1])⊃(255 0 0)(0 0 255)(0 255 0)
     'F1.Average'∆WS'FCol'fcol
     →0
 :CaseList cols←'F1.MB.Focus.'∘,¨'TableSize' 'Function' 'CalcBtn' 'Players'
     target←'F1',{(-(⌽⍵)⍳'.')↑⍵}1⊃args
     ∆NQ target'GotFocus'
     →0
 :Else
     ⎕←'Unexpected message: ',args
 :EndSelect

 :If 0≠⎕NC 'F1.Table'
     'F1.Table'∆WS'Values'(∘.(⍎f)⍨⍳n)
 :EndIf
