 Run web;rc;msg;r;root;seps
 ⍝ Initialise the Demo application
 ⍝ web: 'Desktop' (0), 'Browser' (1) or 'Multi' (2)

 COORD←'Pixel'
 JSWC←##.JSWC
 :If 0<≢r←2 ⎕NQ'.' 'GetEnvironment' 'EWC_PORT'
     JSWC.PORT←⍬⍴2⊃⎕VFI r
 :Else
     JSWC.PORT←22322
 :EndIf
 :If 1≡⊃2⊃⎕VFI 2 ⎕NQ'.' 'GetEnvironment' 'EWC_SHOWDEVTOOLS'
     JSWC.SHOWDEVTOOLS←1
 :Else
     JSWC.SHOWDEVTOOLS←0
 :EndIf
 root←{⊃⎕NPARTS ¯1↓⍵}⊃⎕NPARTS 4⊃5179⌶⊃⎕SI
 JSWC.RESOURCES←1 2⍴'images'(root,'/demo/images')

 ⎕USING←''
 ⍝ seps←System.Globalization.CultureInfo.CurrentCulture.NumberFormat.(NumberDecimalSeparator NumberGroupSeparator)
 ⍝ JSWC.settings.Locale.(Decimal Thousand)←seps

 :Trap 0
     'e'JSWC.Init web
 :Else
     (⊃⎕DMX.DM)⎕SIGNAL⊃⎕DMX.EN
 :EndTrap
 JSWC.NOIDQ←0 ⍝ Allow interactive DQ while running demo

 JSWC.NOIDQ←'Windows'≢7↑⊃'.'⎕WG'APLVersion' ⍝ Allow interactive DQ under Windows
 ⍝ trace into demo functions?
 :If 1≡⊃2⊃⎕VFI 2 ⎕NQ'.' 'GetEnvironment' 'EWC_TRACEDEMOS'
     #.demo.({'Demo'≡4↑⍵:1 ⎕STOP ⍵}¨(⎕NL-3)~'DemoMenu' 'DemoDefault')
 :EndIf

 :Select JSWC.MODE
 :Case 0 ⍝ Desktop
     ⎕SE.Posn←200 300
     DemoMenu 1
     :If JSWC.NOIDQ
        r←eDQ'F1'
     :EndIf

 :Case 1 ⍝ Browser
     ⎕←'Connect Browser now...'
     :Repeat
        ⍞←'.'
        ⎕DL 1
     :Until 2=⎕NC'_JSWC.ID'
     ⎕←''
     :If '''Browser'''≡¯5↑2 ⎕NQ'.' 'GetEnvironment' 'LX'
         ⎕←r←eDQ'.'
         :If r≡'F1' 'Close'
             ⎕←'Bye bye...'
             ⎕DL 3
             ⎕OFF
         :EndIf
         ∘∘∘ ⍝ Abnormal web termination
     :EndIf

 :Case 2 ⍝ Multi
     ⎕←'Running in Multi mode'
 :EndSelect
