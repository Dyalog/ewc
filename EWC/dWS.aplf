 {R}←Name ∆WS Props;WS;msg;ws;i;z;ref;expr;caller;⎕TRAP;proplist;pname;names;values;PropList;m;class;type;ix;HR;e;objref
 R←⍬
 ⎕SIGNAL (2>|≡Props)/11
 Props←,Props
 Name←⍕∊Name

 ⎕TRAP←0 'S'
 LogCalls Name 'WS' Props
 :If (⊂,Name)∊,¨'⎕SE' '#' '.' ⍝ Pass WS of System properties through
    :Trap 0
       →0⊣R←Name ⎕WS Props
    :Else
       'E' Log 'Unable to set ',(,⍕Props),' on ',Name
       →0
    :EndTrap
 :EndIf

 caller←findCaller
 Name←caller removeCaller Name
 (type class objref)←getObj Name
 ('Not a EWC object: ',(⍕caller),'.',Name)⎕SIGNAL (0=≢type)/11

 proplist←⎕C PropList←class.PropList,objProps
 :If (names←⎕C 1↑Props)∊proplist
     values←,⊂1↓Props
 :Else
     :If ∧/(names←⎕C⊃¨Props)∊proplist
         values←1↓¨Props
     :EndIf
 :EndIf

 :If ~∧/m←(≢proplist)≥proplist⍳names
     Warning'Unknown properties: ',(~m)/names
     (names values)←m∘/¨names values
 :EndIf

 :If 0≠≢ix←⍸1=≢¨values
     values[ix]←⊃¨values[ix]
 :EndIf

 names←PropList[proplist⍳names] ⍝ Correct case

 :If ~∧/m←names∊class.Supported,'Event' 'Data'
     Warning'Unsupported properties for type ',type,': ',(~m)/names
     (names values)←m∘/¨names values
 :EndIf

 :If type≡'Form'
 :AndIf 1=≢HR←'HTMLRenderer'⎕WN caller⍎Name
     :If (≢names)≥i←names⍳⊂'Posn'
         (⊃HR)⎕WS'Posn'(i⊃values) ⍝ Move the HTMLRenderer instead
         (names values)←(i≠⍳≢names)∘/¨names values
     :EndIf
     :If (≢names)≥i←names⍳⊂'Size'
         (⊃HR)⎕WS'Size'(HRBorder+i⊃values) ⍝ Resize the HTMLRenderer
     :EndIf
     :If (≢names)≥i←names⍳⊂'Caption'
         (⊃HR)⎕WS'Caption'(i⊃values)
     :EndIf
 :EndIf

 values←names((⎕C type) fixUpProps)values       ⍝ Regularise things for the poor JS code
 :If 0≠i←(1+≢names)-(⌽names)⍳⊂'Event'      ⍝ Last Event
 :AndIf 0 0≢i⊃values                       ⍝ Reset
 :AndIf 0≢⍴e←caller⍎Name,'.Event'          ⍝ Event property is "additive"
     e←((e≢0 0)/e),i⊃values
     (i⊃values)←e[{⊢/⍵}⌸⊃¨e] ⍝ Pick last occurrence of each event
 :EndIf

 WS←⎕NS''
 ws←WS.WS←⎕NS''
 ws.(ID Properties)←Name(⎕NS'')
 :If 0≠≢names
     ⍎'ws.Properties',expr←'.(',(⍕names),')←',(1≠≢values)↓'⊃values'
 :EndIf

 :If USEWIN32
     R←Name caller.⎕WS Props
 :ElseIf 0≠≢names
     expr←'.(',(⍕names),')←',(1≠≢values)↓'⊃values'
     ⍎(⍕caller),'.',Name,expr ⍝ Update "server-side DOM"
 :EndIf

 :If type≡'imagelist'
     ⍎(⍕caller),'.',Name,'.(ImageCount←≢Files)' ⍝ Update read-only ImageCount
 :EndIf

 :If RECORD ⍝ Collecting lists
     ref←⍎'Classes.',Name ⎕WG'Type'
     ref.Supported∪←1⊃¨Props
 :EndIf

 WS.WS.ID←{(2×'#.'≡2↑⍵)↓⍵}WS.WS.ID
  :If (⊂WS.WS.ID)∊⊆1⊃BREAK
     ⎕←'BREAK on WS of ',WS.WS.ID
     ⎕←⎕JSON WS
     ∘∘∘
 :EndIf

 fixSendEvents WS.WS.(ID Properties)
 :If ~0=⍴WS.WS.Properties.⎕NL-2
     sendObject WS WS.WS.ID
 :EndIf
