 mode Log msg;json
⍝ Log modes: Controlled by EWC.LOGMODES
⍝ D: Debug
⍝ E: Error
⍝ F: Format Request
⍝ W: Warning
⍝ T: Transmit on WebSocket
⍝ R: Receive on WebSocket
⍝ C: Connect or Disconnect
⍝ U: Unsupported feature
⍝ w: log eW* object/property access (for testing)
⍝ N: Explicit NQ
⍝ P: ProcessEvent
⍝ G: WG

 →((mode∊LOGMODES)∨('w'∊LOGMODES)∧mode≡'E')↓0
 ((,'ZI2,<:>,ZI2,<.>,ZI3'⎕FMT 1 3⍴¯3↑⎕TS),' ',mode,':')msg
 :If ('w'∊LOGMODES)∧mode≡'E'   ⍝ if an error was logged in mode "w" we need to transform the msg from simple txt to json!
     msg←'{Caller: "N/A", Action:"Error", Type: "Error", Message: "',(⍕msg),'"},'
     mode←'w'   ⍝ change it to enter next If!
 :EndIf

 :If mode≡'w'
    ⍝ primitive: we collect json objects in that file - it's not "json" though...(needs to be enclosed in [])
    ⍝ also we do not care about max file size, we just keep appending
    ⍝ do it's the responsibility of the user to clean up the file.
    ⍝ That's why mode='w' is useful - it's not part of the default (⎕A)
    ⍝ and needs to be set explicitely!
    ⍝ (see eWC Tests for example usage that deletes the file before running tests
    ⍝  and also adds the "[]" )
     msg←('(:.*,)(.*)$'⎕R('\1 TS: [',(∊(⍕¨¯4↑⎕TS),¨','),'],\2')⍠'Greedy' 0)msg
     msg←(¯2↓msg),',Stack: [',(¯1↓∊{'"',(1⊃⍵),'[',(⍕2⊃⍵),']",'}¨↓⎕si,[1.5]⎕lc),']},'
     (⊂msg)⎕NPUT(EWCFolder,'objectlog.json5')2
 :EndIf
