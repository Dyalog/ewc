r←Details classname;class;n;m
:If 9≠##.classes.⎕NC (⎕c classname)
  →0 ⊣ r←(⍪('Not a supported JSWC class: ',classname)'')⍪Classes
:EndIf

class←##.classes⍎⎕C classname

r←('The JSWC implementation of ',class.ClassName,' has some degree of support for:') ''
n←{⍵[⍋⍵]}class.Supported~⊂'Type'
n←' *'[1+m←n∊class.Dynamic],¨n
r,←4 foldCols '  '∘,¨n
r,←(∨/m)/'' '* indicates that the property can change after it has been set.'

:If 2=⎕NC 'class.SupportedEvents'
    n←{⍵[⍋⍵]}class.SupportedEvents
    r,←'' 'Supported events:' '',4 foldCols '  '∘,¨n
:EndIf



:If 2=⎕NC 'class.Limitations'
    r,←'' ('Known limitations of the ',class.ClassName,' implementation:') '',class.Limitations
:EndIf

r←⍪r
