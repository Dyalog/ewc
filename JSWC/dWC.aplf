 {R}←Name ∆WC Props;p;m;WC;msg;length;m2;m3;z;type;class;ref;propnames;temp;proplist;expr;caller;deprecatedprops;props;pn;f;tn;i;ns;okprops
 'NO CLIENT DETECTED' ⎕SIGNAL (USEHR∨2=⎕NC 'CONN')↓11
 deprecatedprops←'Font' 'Icon' 'Cursor' 'ImageList' '3D' ⍝ No longer appear in PropList
 caller←⊃⎕RSI

 Props←,⊆Props
 length←≢¨Props
 propnames←⊃¨Props

 :If 1=≢type←((length∊2)∧propnames∊⊂'Type')/Props ⍝ Named 'Type' property?
     type←2⊃type
 :Else
     type←⊃Props
 :EndIf
 type←⎕C type

:If ~9=classes.⎕NC type
    ('Unsupported class: ',type) ⎕SIGNAL 11
:EndIf

 proplist←(classes⍎type).PropList
 p←(≢Props)↑proplist

 m←~(length∊2 3)∧propnames∊proplist,deprecatedprops
 (m/propnames)←m/p ⍝ Add default prop names
 m2←(~m)∧length=2
 (m2/Props)←2⊃¨m2/Props ⍝ Extract property values
 m3←(~m)∧length=3
 (m3/Props)←1↓¨m3/Props

 :If (≢propnames)≥i←propnames⍳⊂'Event'
     (i⊃Props)←NormaliseEvents i⊃Props
     :If 1=≢i⊃Props
         ⍝ ⎕←'*** Working around event limitation ***'
         ⍝ (i⊃Props)←⊃i⊃Props
     :EndIf
 :EndIf

 WC←⎕NS''
 WC.WC←⎕NS''
 WC.WC.(ID Properties)←Name(⎕NS'')
 ⍎'WC.WC.Properties',expr←'.(',(⍕propnames),')←',(1≠≢propnames)↓'⊃Props'

 :If USEWIN32
     :If 'ribbon'≢6↑type
         props←(m←(type≢'imagelist')∨~propnames∊⍥⎕C ⊂'files')/Props
         pn←m/propnames
         :If type≡'bitmap'
         :AndIf (≢pn)≥i←(⎕C pn)⍳⊂'file'
            (i⊃props)←IMAGEFOLDER,1↓i⊃props ⍝ Adjust BitMap image file names
         :EndIf
         R←Name caller.⎕WC ↓pn,⍪props
         :If 'imagelist'≡type
         :AndIf 2=⎕NC 'WC.WC.Properties.Files' ⍝ Simulate the "Files" property in Win32
             :For f :In WC.WC.Properties.Files
                 tn←(IMAGEFOLDER,1↓f) ⎕NTIE 0
                 (R,'.') (2032⌶) ⎕NREAD tn 83 (⎕NSIZE tn) 0
                 ⎕NUNTIE tn
             :EndFor
         :EndIf
     :EndIf
 :Else
      R←Name caller.⎕NS''
      okprops←proplist,objProps
      :If ∨/m←(≢okprops)<i←okprops⍳propnames
          ('Unsupported properties: ',⍕m/propnames) ⎕SIGNAL 11
      :EndIf
      m←~proplist∊propnames
      expr←'.(',(⍕okprops[i],m/proplist),')←Props,m/classes.',type,'.Defaults' ⍝ /// remove when we go all lowercase everywhere
      ⍎(⍕caller),'.',Name,expr ⍝ Set properties in the "server-side DOM"
 :EndIf

 :If USEHR∧type≡'form'
     ns←caller⍎Name
     :If 'inherit'≡⎕C ns.Coord
         ns.Coord←'Pixel'
     :EndIf

     IGNORE1Init←1 ⍝ Make Handler ignore the next Initialise call
     :If 0≠≢z←MakeHR caller Name
         z ⎕SIGNAL 11
     :EndIf
      WC.WC.Properties.Posn←0 0 ⍝ Form will be at posn 0 0 within the HTLRenderer
 :EndIf

 :If RECORD ⍝ Collecting lists
     :If ~9=⎕NC class←'Classes.',type←WC.WC.Properties.Type
         ref←⍎class ⎕NS''
         ref.PropList←Name Caller.⎕WG'PropList'
         (temp←Name,'_temp')Caller.⎕WC type
         ref.Defaults←temp Caller.⎕WG ref.PropList
         Caller.⎕EX temp
     :EndIf
     ref←⍎class
     ref.Supported∪←propnames
 :EndIf

 msg←⎕JSON⍠'HighRank' 'Split'⊢WC

 :If CONNECTED
     z←CONN WSS.Send ⎕←msg
 :Else
     ⎕←msg
 :EndIf
