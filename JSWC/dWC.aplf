 {R}←Name ∆WC Props;p;m;WC;msg;length;m2;m3;z;type;class;ref;propnames;temp;proplist;expr;caller;deprecatedprops
 deprecatedprops←'Font' 'Icon' 'Cursor' 'ImageList' '3D' ⍝ No longer appear in PropList
 caller←⊃⎕RSI

 :If USEWIN32
     R←Name caller.⎕WC Props
 :Else
     R←Name caller.⎕NS''
 :EndIf

 Props←,⊆ Props
 length←≢¨Props
 propnames←⊃¨Props
 :If 1=≢type←((length∊2)∧propnames∊⊂'Type')/Props ⍝ Named 'Type' property?
     type←2⊃type
 :Else
     type←⊃Props
 :EndIf
 proplist←(Classes⍎type).PropList
 p←(≢Props)↑proplist

 m←~(length∊2 3)∧propnames∊proplist,deprecatedprops
 (m/propnames)←m/p
 m2←(~m)∧length=2
 (m2/Props)←2⊃¨m2/Props
 m3←(~m)∧length=3
 (m3/Props)←1↓¨m3/Props

 WC←⎕NS''
 WC.WC←⎕NS''
 WC.WC.(ID Properties)←Name(⎕NS'')
 ⍎'WC.WC.Properties',expr←'.(',(⍕propnames),')←',(1≠≢propnames)↓'⊃Props'

 :If ~USEWIN32
     ⍎(⍕caller),'.',Name,expr ⍝ Set properties in the "server-side DOM"
 :EndIf

 :If RECORD ⍝ Collecting lists
     :If ~9=⎕NC class←'Classes.',type←WC.WC.Properties.Type
         ref←⍎class ⎕NS''
         ref.PropList←Name Caller.⎕WG'PropList'
         (temp←Name,'_temp')Caller.⎕WC type
         ref.Defaults←temp Caller.⎕WG ref.PropList
         Caller.⎕EX temp
     :EndIf
     ref←⍎class
     ref.Supported∪←propnames
 :EndIf

 msg←⎕JSON⍠ 'HighRank' 'Split'⊢WC

 :If CONNECTED
     z←CONN #.w.Send ⎕←msg
 :Else
     ⎕←msg
 :EndIf
