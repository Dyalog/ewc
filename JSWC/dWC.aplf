 {R}←Name ∆WC Props;p;m;WC;msg;length;m2;m3;z;type;class;ref;propnames;temp;proplist;expr;caller;deprecatedprops;props;pn;f;tn;i;ns;okprops;⎕TRAP;depth;valid;values;lcp;pi;ix;PropList;allprops;AllProps
 ⎕TRAP←0 'S'
 'NO CLIENT DETECTED' ⎕SIGNAL (USEHR∨2=⎕NC 'CONN')↓11
 deprecatedprops←,⊂'3D' ⍝ No longer appear in PropList
 caller←⊃⎕RSI

 Props←,⊆Props
 length←≢¨Props
 depth←|≡¨Props
 propnames←⎕C (depth>1)/¨⊃¨Props
 type←(i←⌊/propnames⍳'' 'type')⊃Props
 :If 2=i⊃depth ⋄ type←2⊃type ⋄ :EndIf
 type←⎕C type
 :If (⊂type)∊⎕C nativeClasses ⍝ Timers and other
     →0 ⊣ R←Name caller.⎕WC Props
 :EndIf

:If ~9=classes.⎕NC type
    Warning 'Unsupported class: ',type
    Name caller.⎕NS ''
    (caller⍎Name).Type←type
    →0
:EndIf

 class←classes⍎type
 proplist←⎕C PropList←class.PropList
 valid←(lcp←⎕C propnames)∊allprops←⎕C AllProps←PropList,objProps,deprecatedprops
 values←valid↓¨Props
 ix←⍸valid∧length=2      ⍝ Prop name found, followed by 1 thing
 values[ix]←⊃¨values[ix]
 ix←⍸0=pi←(1+≢allprops)|allprops⍳lcp
 i←propIndices pi
 propnames[ix]←allprops[i[ix]]
 ix←⍸(lcp←⎕C propnames)∊allprops
 propnames[ix]←AllProps[allprops⍳lcp[ix]] ⍝ Properly cased names
 valid[ix]←1

 :If (≢propnames)≥i←propnames⍳⊂'Event'
     (i⊃values)←NormaliseEvents i⊃values
     :If 1=≢i⊃values
         ⍝ ⎕←'*** Working around event limitation ***'
         ⍝ (i⊃Props)←⊃i⊃Props
     :EndIf
 :EndIf

 WC←⎕NS''
 WC.WC←⎕NS''
 WC.WC.(ID Properties)←Name(⎕NS'')
 ⍎'WC.WC.Properties',expr←'.(',(⍕propnames),')←',(1≠≢propnames)↓'⊃values'

 :If USEWIN32
     :If 'ribbon'≢6↑type
         values←(m←(type≢'imagelist')∨~propnames∊⍥⎕C ⊂'files')/values
         pn←m/propnames
         :If type≡'bitmap'
         :AndIf (≢pn)≥i←(⎕C pn)⍳⊂'file'
            (i⊃props)←IMAGEFOLDER,1↓i⊃props ⍝ Adjust BitMap image file names
         :EndIf
         R←Name caller.⎕WC ↓pn,⍪values
         :If 'imagelist'≡type
         :AndIf 2=⎕NC 'WC.WC.Properties.Files' ⍝ Simulate the "Files" property in Win32
             :For f :In WC.WC.Properties.Files
                 tn←(IMAGEFOLDER,1↓f) ⎕NTIE 0
                 (R,'.') (2032⌶) ⎕NREAD tn 83 (⎕NSIZE tn) 0
                 ⎕NUNTIE tn
             :EndFor
         :EndIf
     :EndIf
 :Else
      R←Name caller.⎕NS''
      okprops←PropList,objProps
      :If ∨/m←(≢class.Supported)<i←class.Supported⍳propnames
          Warning 'Unsupported on ',type,': ',⍕m/propnames
      :EndIf
      m←~PropList∊propnames
      expr←'.(',(⍕propnames,m/PropList),')←values,m/class.Defaults'
      ⍎(⍕caller),'.',Name,expr ⍝ Set properties in the "server-side DOM"
 :EndIf

 :If USEHR∧type≡'form'
     ns←caller⍎Name
     :If 'inherit'≡⎕C ns.Coord
         ns.Coord←'Pixel'
     :EndIf

     IGNORE1Init←1 ⍝ Make Handler ignore the next Initialise call
     :If 0≠≢z←MakeHR caller Name
         z ⎕SIGNAL 11
     :EndIf
      WC.WC.Properties.Posn←0 0 ⍝ Form will be at posn 0 0 within the HTLRenderer
 :EndIf

 :If RECORD ⍝ Collecting lists
     :If ~9=⎕NC class←'Classes.',type←WC.WC.Properties.Type
         ref←⍎class ⎕NS''
         ref.PropList←Name Caller.⎕WG'PropList'
         (temp←Name,'_temp')Caller.⎕WC type
         ref.Defaults←temp Caller.⎕WG ref.PropList
         Caller.⎕EX temp
     :EndIf
     ref←⍎class
     ref.Supported∪←propnames
 :EndIf

 msg←⎕JSON⍠'HighRank' 'Split'⊢WC

 :If CONNECTED
     z←CONN WSS.Send ⎕←msg
 :Else
     ⎕←msg
 :EndIf
