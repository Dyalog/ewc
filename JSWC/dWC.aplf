 {R}←Name ∆WC Props;p;m;WC;msg;length;m2;m3;z;type;class;ref;propnames;temp;proplist;expr;caller;deprecatedprops;props;pn;f;tn
 deprecatedprops←'Font' 'Icon' 'Cursor' 'ImageList' '3D' ⍝ No longer appear in PropList
 caller←⊃⎕RSI

 Props←,⊆Props
 length←≢¨Props
 propnames←⊃¨Props

 :If 1=≢type←((length∊2)∧propnames∊⊂'Type')/Props ⍝ Named 'Type' property?
     type←2⊃type
 :Else
     type←⊃Props
 :EndIf
 type←⎕C type

 proplist←(Classes⍎type).PropList
 p←(≢Props)↑proplist

 m←~(length∊2 3)∧propnames∊proplist,deprecatedprops
 (m/propnames)←m/p ⍝ Add default prop names
 m2←(~m)∧length=2
 (m2/Props)←2⊃¨m2/Props ⍝ Extract property values
 m3←(~m)∧length=3
 (m3/Props)←1↓¨m3/Props

 WC←⎕NS''
 WC.WC←⎕NS''
 WC.WC.(ID Properties)←Name(⎕NS'')
 ⍎'WC.WC.Properties',expr←'.(',(⍕propnames),')←',(1≠≢propnames)↓'⊃Props'

 :If USEWIN32
     :If 'ribbon'≢6↑type
         props←(m←(type≢'imagelist')∨~propnames∊⍥⎕C ⊂'files')/Props
         pn←m/propnames
         R←Name caller.⎕WC ↓pn,⍪props
         :If 'imagelist'≡type
         :AndIf 2=⎕NC 'WC.WC.Properties.Files' ⍝ Simulate the "Files" property in Win32
             :For f :In WC.WC.Properties.Files
                 tn←(IMAGEFOLDER,1↓f) ⎕NTIE 0
                 (R,'.') (2032⌶) ⎕NREAD tn 83 (⎕NSIZE tn) 0
                 ⎕NUNTIE tn
             :EndFor
         :EndIf
     :EndIf
 :Else
      R←Name caller.⎕NS''
      ⍎(⍕caller),'.',Name,expr ⍝ Set properties in the "server-side DOM"
 :EndIf

 :If RECORD ⍝ Collecting lists
     :If ~9=⎕NC class←'Classes.',type←WC.WC.Properties.Type
         ref←⍎class ⎕NS''
         ref.PropList←Name Caller.⎕WG'PropList'
         (temp←Name,'_temp')Caller.⎕WC type
         ref.Defaults←temp Caller.⎕WG ref.PropList
         Caller.⎕EX temp
     :EndIf
     ref←⍎class
     ref.Supported∪←propnames
 :EndIf

 msg←⎕JSON⍠'HighRank' 'Split'⊢WC

 :If CONNECTED
     z←CONN #.w.Send ⎕←msg
 :Else
     ⎕←msg
 :EndIf
