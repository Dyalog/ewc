 r←con Handler payload;ns;Event;ID;Info;events;fns;fn;event;reqfn;info;sns;i;code;conn;coni;sock;z;m;msg;sessionspace;form;hr;CALLER;app;new;old;token
 ⎕TRAP←0 'S'
 'R'Log'#',(⍕con),': ',payload
 i←(2⊃SESSIONS)⍳con
 :If i>≢1⊃SESSIONS
     'W'Log'Message received from closed session #',⍕con
     →0
 :EndIf
 code←(sessionspace←(2+i)⊃SESSIONS).code

 :If (⊂10↑payload)∊'Initialise' 'Initialize'
     :If 2=⎕NC'IGNORE1Init'
     :AndIf IGNORE1Init≠0
         IGNORE1Init←0
         :Return
     :EndIf
     fn←reqfn←(10↓payload)~'()'
     fn,←(0=≢fn)/'Initialise'
     app←code
     :If 0≠app.⎕NC 'workNamespace' ⍝ /// change this name in DWI
        app←app.workNamespace
     :EndIf
     :If 3=app.⎕NC fn
         (app⍎fn)&CONNECTED
     :ElseIf 0≠≢reqfn ⍝ Explicit function requested
         'E'Log'Initialise: Function not found: ',(⍕code),'.',fn
     :EndIf
 :Else
     :Trap 999
         :If 0=⎕NC 'code._JSWC'
             'E' Log '_JSWC not found in ',⍕code
             →0
         :EndIf
         sns←code._JSWC ⍝ Session Namespace
         ns←0 ⎕JSON payload

         :Select ,ns.⎕NL 9
         :Case 'Event'
             ns.Conn←con
             ns ⎕TPUT token←DQ_TOKENBASE+TOKENSTEP×1⌈sns.ID×MODE=2
             'N' Log 'NQ of token ',(⍕token),': ',⎕JSON ns
             :Hold 'JSWC_DQTOKENS'
                 :If 0=≢DQTOKENS ⍝ No active DQ
                    'N' Log '    Poking ',(⍕sns),'.TIMER'
                    sns.⎕NQ'TIMER' 1001 ⍝ Wake it up
                 :EndIf
             :EndHold
         :CaseList 'WG' 'WX'
             ns ⎕TPUT WG_TOKENBASE+TOKENSTEP×1⌈sns.ID×MODE=2 ⍝ ⊃getConnection sns ns.WG.ID
         :Case 'FormatCell'
             :If 0≠≢msg←code formatCell ns
                 'T'Log('#',⍕con),': ',msg
                 z←con WSS.Send msg
             :EndIf
         :Case 'Signal'
             :If ns.Signal.Name≡'Close' ⍝ Client about to close the socket
                 closeConnection con
             :Else
                 'E'Log'Unknown signal received: ',payload
             :EndIf
         :Case 'DeviceCapabilities'
             devCaps←sessionspace.devCaps←(0 ⎕JSON payload).DeviceCapabilities
             'code._JSWC' ⎕NS 'devCaps'
             :If MODE=0 ⍝ Desktop
                 :For form :In 'Form'∆WN code
                     :If 9=⎕NC form,'.HR'
                         hr←⍎form,'.HR'
                         CALLER←code
                         old←⍎form,'.(Posn,Size)'
                         new←hr.Posn,hr.Size-HRBorder
                         :If old≢new
                             ∆NQ form 'Configure',new
                         :EndIf
                     :EndIf
                 :EndFor
             :EndIf
         :Else
             'E'Log'Invalid message received: ',payload
         :EndSelect
     :Else
         'E'Log'Message processing failed:'
         'E'Log←↑⎕DMX.DM
     :EndTrap
 :EndIf
