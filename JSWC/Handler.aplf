r←con Handler payload;ns;Event;ID;Info;events;fns;fn;event;z;reqfn;info;sns;i;code
 ⎕TRAP←0 'S'
 ⎕←(,'ZI2,<:>,ZI2,<.>,ZI3' ⎕FMT 1 3⍴¯3↑⎕TS),' Recv #',(⍕con),': ',payload
 i←(2⊃SESSIONS)⍳con
 :If i>≢1⊃SESSIONS
     ⎕←'Message received from closed session #',⍕con
     →0
 :EndIf
 code←(2+i)⊃SESSIONS

 :If (⊂10↑payload)∊'Initialise' 'Initialize'
     :If 2=⎕NC 'IGNORE1Init'
     :AndIf IGNORE1Init≠0
         IGNORE1Init←0
         ⎕←'Ignoring one Initialise'
         :Return
     :EndIf
     fn←reqfn←(10↓payload)~'()'
     fn,←(0=≢fn)/'Initialise'
     :If 3=code.⎕NC fn
         (code⍎fn)&CONNECTED
     :ElseIf 0≠≢reqfn ⍝ Explicit function requested
         ⎕←'Function not found: ',(⍕code),'.',fn
     :EndIf
 :Else
     :Trap 999
         sns←code._JSWC ⍝ Session Namespace
         ns←0 ⎕JSON payload
         :Select ,ns.⎕NL 9
         :Case 'Event'
             ⍝ /// Maybe use a "proper" queue
             ns ⎕TPUT DQ_TOKENBASE+TOKENSTEP×sns.ID
             ⍝ /// Need something better than this from JD
             :If '*'∊1↓⎕STACK[;2] ⍝ A suspension in the client app
             :AndIf 9=sns.⎕NC 'TIMER'
                  sns.⎕NQ 'TIMER' 1001 ⍝ Wake it up
             :EndIf
         :Case 'WG'
             ns ⎕TPUT WG_TOKENBASE+TOKENSTEP×sns.ID
         :Else
             ⎕←'Invalid message received: ',payload
         :EndSelect
     :Else
         ⎕←'Message processing failed:'
         ⎕←↑⎕DMX.DM
     :EndTrap
 :EndIf
