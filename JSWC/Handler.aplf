r←con Handler payload;ns;Event;ID;Info;events;fns;fn;event;z
 ⎕TRAP←0 'S'
 :If (⊂10↑payload)∊'Initialise' 'Initialize'
     CONN←con
     CONNECTED←1

     fn←(10↓payload)~'()'
     fn,←(0=≢fn)/'Initialise'
     :If 3=CODELOCATION.⎕NC fn
         (CODELOCATION⍎fn)&CONNECTED
     :Else
         ⎕←'Function not found: ',(⍕CODELOCATION),'.',fn
     :EndIf
 :Else
     :Trap 999
         ⎕←'Received: ',payload
         ns←0 ⎕JSON payload
         :Select ,ns.⎕NL 9
         :Case 'Event'
             (Event ID Info)←ns.Event.(EventName ID({0::⍵ ⋄ Info}⍬))
             :If (1=≢Event)∧2=≡Event
                 ⎕←'*** Event is incorrectly sent as a list'
                 Event←⊃Event
             :EndIf
             :If (2=≢Event)∧2=≡Event
                 ⎕←'*** Client returned a 2-element event: ',⍕Event
                 Event←⊃Event
             :EndIf

             :Trap 6
                 event←CODELOCATION⍎z←ID,'.event'
             :Else
                 ⎕←'*** Unable to find ',z
                 →0
             :EndTrap

             (events fns)←↓⍉↑event
             :If ~(⊂Event)∊events
                 ∘∘∘
                 ⎕←'*** No callback defined for event ',Event,' on object ',ID
                 →0
             :EndIf

             :If 3≠CODELOCATION.⎕NC fn←(events⍳⊂Event)⊃fns
                 ⎕←'Callback function not found: ',fn
             :Else
                 (CODELOCATION⍎fn)&(ID Event),Info
             :EndIf
         :Case 'WG'
             ns ⎕TPUT WG_TOKEN
         :Else
             ⎕←'Invalid message received: ',payload
         :EndSelect
     :Else
         ⎕←'Message processing failed:'
         ⎕←↑⎕DMX.DM
     :EndTrap
 :EndIf
