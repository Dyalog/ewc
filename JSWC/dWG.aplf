 {R}←Name ∆WG Props;msg;WG;wg;z;⎕TRAP;caller
 caller←⊃⎕RSI
 WG←⎕NS''
 wg←WG.WG←⎕NS''
 WGID+←1
 wg.(WGID ID Properties)←(⍕WGID)Name(,⊆Props)
 msg←⎕JSON WG

 :If CONNECTED
     z←CONN ##.w.Send ⎕←msg
 :Else
     ⎕←msg
 :EndIf

 :If USEWIN32
     R←Name caller.⎕WG wg.Properties
 :Else
     R←caller⍎Name,'.(',(⍕Props),')'
     :If 1=≡Props
         R←,⊂R
     :EndIf
 :EndIf


 :If USEWIN32∧~CONNECTED
     wg.Properties←⎕NS''
     ⍎'wg.Properties.(',(⍕Props),')←',(1≠≢R)↓'⊃R'
     msg←⎕JSON⍠'HighRank' 'Split'⊢WG
     ⎕←'Faking client transmission: ',msg
     (⎕JSON msg)⎕TPUT WG_TOKEN
 :EndIf

 R←WaitForWG Props
