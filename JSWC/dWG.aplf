 {R}←Name ∆WG Props;msg;WG;wg;z;caller;p;class;remote;dynamic;r;d;i;⎕TRAP;proplist;PropList;m;ix;ID;conn
 ⎕TRAP←0 'S'
 Name←⍕Name
 :If (⊂,Name)∊,¨'⎕SE' '#' '.' ⍝ Pass WG of System properties through
     →0 ⊣ R←Name ⎕WG Props
 :EndIf

 caller←⊃⎕RSI
 (ID conn)←getConnection caller
 WG←⎕NS''
 wg←WG.WG←⎕NS''
 WGID+←1
 p←,⊆Props
 :Trap 2 6
     class←GetClass caller Name
 :Else
     (⊃⎕DMX.DM)⎕SIGNAL ⎕DMX.EN
 :EndTrap

 proplist←⎕C PropList←class.PropList,objProps
 :If ∨/m←(≢proplist)<i←proplist⍳⎕C p
     Warning 'Unsupported on ',class,': ',⍕m/p
 :EndIf
 p[ix]←PropList[i[ix←⍸~m]]

 :If remote←∨/,dynamic←p∊⍥⎕C class.Dynamic,⊂'NoSuchProperty' ⍝ Any reason to ask remote client for values?
     wg.(WGID ID Properties)←(⍕WGID) ((2×'#.'≡2↑Name)↓Name) (d←dynamic/p)
     msg←⎕JSON WG
     :If CONNECTED
         z←conn WSS.Send ⎕←msg
     :Else
         ⎕←msg
     :EndIf
 :EndIf

 :If USEWIN32
     R←Name caller.⎕WG p
 :Else
     R←(≢p)⍴⊂⍬
     :If 0≠≢i←⍸~dynamic
        R[i]←caller⍎((1=≢i)/'⊂'),Name,'.(',(⍕p[i]),')'
     :EndIf
 :EndIf

 :If remote∧USEWIN32∧~CONNECTED
     wg.Properties←⎕NS''
     r←dynamic/R
     ⍎'wg.Properties.(',(⍕d),')←',(1≠≢r)↓'⊃r'
     msg←⎕JSON⍠'HighRank' 'Split'⊢WG
 ⍝   ⎕←'Faking client transmission: ',msg
     (⎕JSON msg)⎕TPUT WG_TOKENBASE+TOKENSTEP×ID
 :EndIf

 :If remote
     :Trap 6 ⍝ Request for unsupported property
        r←msg WaitForWG d ID (⍕WGID)
        R[p⍳d]←r
     :Else
        ⎕DMX.EM ⎕SIGNAL ⎕DMX.EN
     :EndTrap
 :EndIf

 :If 1=≡Props
    R←⊃R
 :EndIf
