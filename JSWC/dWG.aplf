 {R}←Name ∆WG Props;msg;WG;wg;z;⎕TRAP;caller;p;class;remote;dynamic;r;d;i
 caller←⊃⎕RSI
 WG←⎕NS''
 wg←WG.WG←⎕NS''
 WGID+←1
 p←,⊆Props
 :Trap 6
     class←GetClass caller Name
 :Else
     (⊃⎕DMX.DM)⎕SIGNAL ⎕DMX.EN
 :EndTrap

 :If remote←∨/,dynamic←p∊⍥⎕C class.Dynamic,⊂'NoSuchProperty' ⍝ Any reason to ask remote client for values?
     wg.(WGID ID Properties)←(⍕WGID)Name (d←dynamic/p)
     msg←⎕JSON WG
     :If CONNECTED
         z←CONN ##.w.Send ⎕←msg
     :Else
         ⎕←msg
     :EndIf
 :EndIf

 :If USEWIN32
     R←Name caller.⎕WG p
 :Else
     R←(≢p)⍴⊂⍬
     :If 0≠≢i←⍸~dynamic
        R[i]←caller⍎((1=≢i)/'⊂'),Name,'.(',(⎕C ⍕p[i]),')'
     :EndIf
 :EndIf

 :If remote∧USEWIN32∧~CONNECTED
     wg.Properties←⎕NS''
     r←dynamic/R
     ⍎'wg.Properties.(',(⍕d),')←',(1≠≢r)↓'⊃r'
     msg←⎕JSON⍠'HighRank' 'Split'⊢WG
     ⎕←'Faking client transmission: ',msg
     (⎕JSON msg)⎕TPUT WG_TOKEN
 :EndIf

 :If remote
     r←msg WaitForWG d (⍕WGID)
     R[p⍳d]←r
 :EndIf

 :If 1=≡Props
    R←⊃R
 :EndIf
