 R←msg WaitForWG(Props WGID);j;⎕TRAP;p;seqok;nl;nlc

AGAIN:
 j←WG_TIMEOUT ⎕TGET WG_TOKEN
 :If 0=≢j
     ⎕←'Client did not respond within ',(⍕WG_TIMEOUT),' seconds.'
     ⎕←'Request was: ',msg
     ⎕TRAP←0 'S' ⋄ ∘∘∘
 :EndIf

 nl←⍬
 :If 9=⎕NC'j.WG'
 :AndIf seqok←j.WG.{0=⎕NC'WGID':1 ⋄ WGID≡⍵}WGID
 :AndIf ∧/(p←⎕C ,⊆Props)∊nlc←⎕C nl←j.WG.Properties.⎕NL-2
     p←nl[nlc⍳p] ⍝ /// Should go when everything is lowercase
     R←j.WG.Properties⍎((1=≢p)/',⊂'),⍕p
     R←j.WG.ID MixTables p R
     :If 1=≡Props
         R←⊃R ⍝ A single simple property
     :EndIf
 :Else
     :If ~seqok
         ⎕←'Ignoring WGID ',j.WG.WGID,' while waiting for ',WGID
         →AGAIN
     :EndIf
     ⎕←'*** ∆WG return did not contain values for:',⍕Props~nl
     R←(⊂⍬)⍨¨Props
 :EndIf
