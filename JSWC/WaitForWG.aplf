 R←msg WaitForWG(Props WGID);j;⎕TRAP;p;seqok;nl;nlc;m;t;token

 token←WG_TOKENBASE+TOKENSTEP×CODELOCATION._JSWC.ID
AGAIN:
 j←WG_TIMEOUT ⎕TGET token
 :If 0=≢j
     ⎕←'Client did not respond within ',(⍕WG_TIMEOUT),' seconds.'
     ⎕←'Request was: ',msg
     ⎕SIGNAL 6
 :EndIf

 nl←⍬
 :If 9=⎕NC'j.WG'
 :AndIf seqok←j.WG.{0=⎕NC'WGID':1 ⋄ WGID≡⍵}WGID
 :AndIf ∧/(p←⎕C ,⊆Props)∊nlc←⎕C nl←j.WG.Properties.⎕NL-2
     p←nl[nlc⍳p] ⍝ /// Should go when everything is lowercase
     R←j.WG.Properties⍎((1=≢p)/',⊂'),⍕p
     R←j.WG.ID MixTables p R
     :If 1=≡Props
         R←⊃R ⍝ A single simple property
     :EndIf
 :Else
     :If ~seqok
         ⎕←'Ignoring WGID ',j.WG.WGID,' while waiting for ',WGID
         →AGAIN
     :EndIf
     :If 2=⎕NC 'j.WG.NotSupported'
     :AndIf ∨/m←(t←Props~nl)∊j.WG.NotSupported
         ('Property not supported by JSWC: ',⍕m/t) ⎕SIGNAL 6
     :EndIf
 :EndIf
