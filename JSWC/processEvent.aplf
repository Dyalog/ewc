 {end}←processEvent ns;Info;ID;Event;event;events;fns;info;fn;z;async;class;e;code;res;gui;shift;k

 :If 2=ns.⎕NC'CodeLocation'
     code←⍎ns.CodeLocation
 :Else
     code←CODELOCATION
 :EndIf

 :If 9=code.⎕NC '_JSWC_GUI_NS'
     gui←code._JSWC_GUI_NS
 :Else
     gui←code
 :EndIf

 end←0
 (Event ID Info)←ns.Event.(EventName ID({0::⍵ ⋄ Info}⍬))

 :If Event≡'KeyPress'
 :AndIf (≢JSKeys)≥z←JSKeys⍳Info[1]
     shift←4⊃Info
     Info[1 2]←((shift≤2)/k←⊃APLKeys[z;1+2⌊4⊃Info]) 0
     →(0≡k)⍴0 ⍝ Ignore KeyPress with 0 in APLKeys (Shift/Ctrl & friends)
 :EndIf

 :Trap 6
     event←gui⍎z←ID,'.Event'
 :Else
     'E'Log'*** Unable to find ',z,', cannot process ',⎕JSON ns
     →0
 :EndTrap

 (events fns)←↓⍉↑event
 :If ~(⊂Event)∊events
     'E'Log'*** No callback defined for event ',Event,' on object ',ID
     →0
 :EndIf

 fn←⊃info←(events⍳⊂Event)⊃fns
 →((fn≡¯1)∨end←fn≡1)⍴0

 async←'&'=¯1↑fn
 fn←(⍕code),'.',(-async)↓fn
 res←⍬

 :Hold 'JSWC.processEvent'
     :If (⊂Event)∊⊆2⊃BREAK
     :OrIf (⊂ID)∊⊆1⊃BREAK
         ⎕←'BREAK on ',⎕JSON ns
         ∘∘∘
     :EndIf

     :If 3≠⎕NC fn
         'E'Log'Callback function not found: ',fn
     :ElseIf 1=≢info
         :If async
             (⍎fn)&(ID Event),Info
         :ElseIf 0=1 2⊃⎕AT fn ⍝ Niladic fn
             ⍎fn
         :Else
             :Trap 85 ⍝ No result
                 res←0(85⌶)fn,'(ID Event),Info'
             :EndTrap
         :EndIf
     :ElseIf 2=≢info
         :If async
             (2⊃info)(⍎fn)&(ID Event),Info
         :Else
             :Trap 85
                res←0(85⌶)'(2⊃info)',fn,'(ID Event),Info'
             :EndTrap
         :EndIf
     :Else
         'E'Log'*** Ill-formed event: ',⍕event
     :EndIf
 :EndHold

 :If res≢⍬
 ⍝ :If ~(⊂res)∊⍬ 1 (,1)
 :AndIf res≢ID Event,Info
     'D'Log'Callback function ',fn,' returned modified event: ',⍕res
 :EndIf

 :If 1 ⍝ Recording events
 :AndIf 9=⎕NC(⍕code),'.',ID
 :AndIf 9=⎕NC class←'classes.',⎕C code⍎ID,'.Type'
     class←⍎class
     e←{0::⍵ ⋄ class.SupportedEvents}''
     :If ~(⊂Event)∊e
     :AndIf isChar Event
         'D'Log'Adding event ',Event,,' to ',⍕class
         class.SupportedEvents←∪e,⊂Event
         ⎕SE.Link.Add(⍕class),'.SupportedEvents'
     :EndIf
 :EndIf
