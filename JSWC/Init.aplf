 {prefix}Init args;mode;z;i;t;fn;folder;⎕TRAP;DPR_FORM;_JSWC
 ⎕IO←⎕ML←1

 ⎕TRAP←0 'S'

 :If 80=⎕DR args ⋄ args←⊆args ⋄ :EndIf
 :Select ≢args←,args
 :Case 1 ⋄ mode←⊃args ⋄ CODELOCATION←⊃⎕RSI
 :Case 2 ⋄ (mode CODELOCATION)←args
 :Else
     'Right agument: mode [codelocation]'⎕SIGNAL 5
 :EndSelect

 :If 0=⎕NC'prefix'
     prefix←'∆'
 :EndIf

 :If ~(⊂mode)∊0 1 2
     mode←¯1+'desktop' 'browser' 'multi'⍳⊂⎕C mode,(0=≢mode)/'desktop'
     'Mode argument should be one of Desktop/0, Browser/1 or Multi/2'⎕SIGNAL(mode=3)/11
 :EndIf

 USEHR←mode=0
 MODE←mode

 :Trap 0
     z←1÷CODELOCATION ⎕THIS≡⍎⍕CODELOCATION ⎕THIS ⍝ Will DOMAIN ERROR if not OK
 :Else
     'JSWC only works with named namespaces'⎕SIGNAL 11
 :EndTrap

 TOOLSPACES←⎕THIS,{0::⍬ ⋄ ∪#.DWI∩⎕RSI}⍬

 :If mode=2
     :If CODELOCATION∊# ⎕SE
     :OrIf 3≠CODELOCATION.⎕NC'Init'
         'Multi-mode must be initialised from a namespace containing a function Init'⎕SIGNAL 11
     :EndIf
 :EndIf


 JSWCFolder←⊃⎕NPARTS 4⊃5179⌶⊃⎕SI

 :If 0=⎕NC'JSClientFolder'
     :If ⎕NEXISTS folder←'jswc-client/dist/',⍨{⊃⎕NPARTS ¯1↓⍵}⍣2⊢JSWCFolder
         JSClientFolder←folder
     :Else
         'Unable to locate JSWC Client'⎕SIGNAL 11
     :EndIf
 :EndIf

 :If 0=⎕NC'MAXLOG'
     MAXLOG←2000
 :EndIf
 :If 0=⎕NC'LOGMODES'
     LOGMODES←⎕A ⍝ Log everything (see Log)
 :EndIf
 :If 0=⎕NC'RESOURCES'
     RESOURCES←1 2⍴⊂''
 :EndIf
 :If 0=⎕NC'PORT'
     PORT←22322
 :EndIf
 :If 0=⎕NC'MAXSESSIONS'
     MAXSESSIONS←100
 :EndIf
 :If 0=⎕NC'BREAK'
     BREAK←⍬ ⍬ ⍝ IDs Events
 :EndIf
 :If 0=⎕NC'SHOWDEVTOOLS'
     SHOWDEVTOOLS←0
 :EndIf

⍝ Establish API in calling space unless prefix≡''
 :If 0≠≢prefix
     :For fn :In 'WC' 'WS' 'WG' 'WN' 'NQ' 'EX' 'DQ'
         ⍎(⍕CODELOCATION),'.',prefix,fn,'←',(⍕⎕THIS),'.∆',fn
     :EndFor
 :EndIf

 CONNECTED←1 ⍝ /// This should go
 RECORD←0
 USEWIN32←0

 :If MODE≠2 ⍝ Only going to have 1 instance of codelocation
     make_JSWC CODELOCATION ⍝ So create the timer here
 :EndIf

 :If 9=⎕NC 'WSS'
     ⎕←'Closing existing Web Socket Server...'
     WSS.Stop
     ⎕DL 5
 :EndIf

 WSS←wss.New''
 :If 9=⎕NC'#.DRC'
     WSS.LDRC←#.DRC
 :EndIf
 WSS.HTMLFolder←JSClientFolder
 WSS.ShowHTMLRenderer←0
 WSS.Aliases←RESOURCES
 WSS.OnConnect←''
 WSS.OnWSUpgrade←(⍕⎕THIS),'.newSession'
 WSS.OnHTTPHeader←''
 WSS.OnWSReceive←''
 WSS.OnClose←(⍕⎕THIS),'.endSession'
 WSS.OnTimeout←(⍕⎕THIS),'.onTimeout'
 WSS.Handler←'Handler'
 WSS.Port←PORT
 WSS.WaitTimeout←500

 allocateTokens
 DQTOKENS←⍬      ⍝ No tokens currently being DQ's

 HRBorder←40 17  ⍝ Additional space required for HR chrome
 MENUOffset←23 0 ⍝ Space to allow for menu
 NOIDQ←1      ⍝ No interactive DQ
 WGID←0
 WG_TIMEOUT←3 ⍝ Seconds to wait for response
 SESSIONS←⍬ ⍬ ⍝ (IDs, Connections, SessionData)
 QUEUEIDS←QUEUEDATA←⍬

 (EVENTNUMS EVENTNAMES)←↓⍉EVENTS
 eventNames←⎕C EVENTNAMES

 (JSKeys APLKeys)←1 1 0 0⊂keyMap ⋄ JSKeys←,JSKeys

 z←WSS.Start 0
 :If 0≠⊃⊃z
     (⍕z)⎕SIGNAL 11
 :EndIf

 :If 4≤⍴devCaps←'.' ⎕WG 'DevCaps'
     DPR←100÷⍨4⊃devCaps
 :Else
     DPR←1
 :EndIf
