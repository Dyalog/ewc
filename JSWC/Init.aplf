 {prefix}Init code;z;i;t;fn

 :Trap 0
     :If 2=⎕NC'code'
         code←⍎code
     :EndIf
     z←1÷code≡⍎⍕code ⍝ Will DOMAIN ERROR if not OK
 :Else
     'Code location must be a properly named namespace'⎕SIGNAL 11
 :EndTrap

 :If 0=⎕NC'prefix'
     prefix←'∆'
 :EndIf

 :If 0=⎕NC 'RESOURCES'
     RESOURCES←1 2⍴⊂''
 :EndIf
 :If 0=⎕NC 'PORT'
     PORT←22322
 :EndIf

⍝ Establish API in calling space
 :For fn :In 'WC' 'WS' 'WG' 'WN' 'NQ' 'EX' 'DQ'
     ⍎(⍕code),'.',prefix,fn,'←',(⍕⎕THIS),'.∆',fn
 :EndFor

 CONNECTED←0 ⍝ /// This should go
 USEHR←1
 RECORD←0
 USEWIN32←0

 JSWCFolder←⊃⎕NPARTS 4⊃5179⌶⊃⎕SI
 CODELOCATION←code

 WSS←wss.New''
 :If 9=⎕NC '#.DRC'
     WSS.LDRC←#.DRC
 :EndIf
 WSS.ShowHTMLRenderer←0
 WSS.HTMLFolder←JSWCFolder,'server/'
 WSS.Aliases←RESOURCES
 WSS.OnConnect←'onConnect'
 WSS.OnWSUpgrade←'onWSUpgrade'
 WSS.OnTimeout←'onTimeout'
 WSS.OnWSReceive←'Handler'
 WSS.OnClose←'onClose'
 WSS.Handler←'Handler'
 WSS.Port←PORT

 t←'JSWC Synchronisation'

 :Trap 2 ⍝ SYNTAX ERROR in v18.2
     z←↑⎕TALLOC 0
     :If (≢z)≥i←z[;2]⍳⊂t
         TOKENRANGE←z[i;1]
     :Else
         TOKENRANGE←⎕TALLOC 1 t
     :EndIf
     WG_TOKEN←TOKENRANGE+0.1
 :Else
     WG_TOKEN←1818
 :EndTrap

 WGID←0
 WG_TIMEOUT←5 ⍝ Seconds to wait for response

 (EVENTNUMS EVENTNAMES)←↓⍉↑2031⌶0

 z←WSS.Start 0
 :If 0≠⊃⊃z
     (⍕z) ⎕SIGNAL 11
 :EndIf
