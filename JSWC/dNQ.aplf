 {R}←{Mode}∆NQ Event;caller;NQ;nq;msg;z;⎕TRAP;ID;conn;name;tkn;type;class;objref;event;i;fn;ok;hr;hdr;posn;size;methods;method
 R←⍬
 :If ⎕THIS≡caller←⊃⎕RSI
     caller←CALLER ⍝ // This is a hack to deal with ∆NQ from within Handler
 :EndIf
 name←⊃Event
 (ID conn)←getConnection caller name

 (⊃Event)←caller removeCaller name ⍝ (2×'#.'≡2↑⊃Event)↓⊃Event

 :If 0=⎕NC'Mode' ⋄ Mode←0 ⋄ :EndIf
 :If (,¨1↑Event)∊,¨'.' '#' '⎕SE' ⍝ Pass NQ to system objects through
     →0⊣R←Mode ⎕NQ Event
 :EndIf

 (type class objref)←getObj name
 ('Not a JSWC object: ',(⍕caller),'.',name)⎕SIGNAL(0=≢type)/11

 method←(⎕C Event[2])∊class.methodlist ⍝ Method rather than property

 :If 1001≡Event[2]
     ∘∘∘
 :EndIf

 :If (Mode∊1 2)∧Event[2]∊supportedMethods
     :Trap 6
         →0⊣R←dWX Event
     :Else
        :Select ⎕C 2⊃Event
        :Case 'gettextsize'
           'W' Log '*** Faking GetFocus'
           →0 ⊣ R←10×⍴⎕FMT 3⊃Event
        :Case 'getfocus'
           'W' Log '*** Faking GetFocus'
           →0 ⊣ R←⊃Event ⍝ Just return form name
        :EndSelect
     :EndTrap
 :EndIf

 :If ~Mode∊0 2
     'This NQ currently not supported' ⎕SIGNAL 11
 :EndIf

 event←objref.Event

 :If (≢event)≥i←(⊃¨event)⍳Event[2] ⍝ Object has an event handler defined
     fn←ok←1 ⍝ Action not prohibited
     :If (≢event)≥i←⌊/(⊃¨event)⍳'Close' 'Configure'∩Event[2] ⍝ Special events
         fn←⊃(i,2)⊃event
         :If isChar fn
         :AndIf 3=caller.⎕NC fn
             :Trap 85
                 ok←0(85⌶)(⍕caller),'.',fn,'(⊂caller removeCaller ⊃Event),1↓Event'
             :EndTrap
         :EndIf
     :EndIf

     :If (¯1≡⊃fn)∨ok≡0 ⍝ Inhibited by property or refused by callback
         →0
     :EndIf
 :EndIf

 :If (MODE=0)∧type≡'Form' ⍝ Need special handling of some Form events when hosted in an HTMLRenderer
     hr←objref.HR
     :Select ⎕C 2⊃Event
     :Case 'close'
         ∆EX name
         →0
     :Case 'configure'
         (hdr posn size)←1 0 1 0 1 0⊂6↑Event
         objref.HR.(Posn Size)←0 (HRBorder)+posn size
         Event[3 4]←MENUOffset ⍝ We always tell the client that Posn is (0,0) + Offset for Menu
     :EndSelect
 :EndIf

 NQ←⎕NS''
 nq←NQ.NQ←⎕NS''
 nq.(ID Event Info NoCallback)←(2↑Event),(2↓Event) (Mode=2)

 :If USEWIN32
     R←Mode caller.⎕NQ Event
 :EndIf

 :If (~isChar nq.Event)∨(⊂nq.Event)∊⊂'Close' ⍝ /// This needs to become a configurable list
     nq.EventName←2⊃Event
     nq.⎕EX'Event'
     NQ.Event←nq
     NQ.⎕EX'NQ'
     tkn←DQ_TOKENBASE+TOKENSTEP×1
     :If 'N'∊LOGMODES
         'N'Log'NQ: ',msg←1 ⎕JSON⍠'HighRank' 'Split'⊢NQ
     :EndIf
     →0⊣NQ ⎕TPUT tkn
 :EndIf

 msg←⎕JSON⍠'HighRank' 'Split'⊢NQ

 :If CONNECTED
     z←conn WSS.Send msg
     'T'Log msg
 :Else
     ⎕←msg
 :EndIf
