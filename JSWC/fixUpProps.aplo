 values←names(type fixUpProps)values;special;i;ixs;ns;n;ix;val;c
⍝ Massage properties that can have multiple formats into regular structures for JS

 special←'Event' 'Points' 'Radius' 'LWidth' 'LStyle' 'FStyle' 'Visible' 'Active' 'Interval' 'File' 'FileNames' 'Picture'
 :If (⊂type)∊'rect' 'circle' 'ellipse' 'poly' 'font'
     special,←'FCol' 'BCol' 'FillCol' 'ArcMode' 'Start' 'End' 'Size'
 :EndIf

 i←⍸names∊special
 (ns ixs)←↓⍉names[i]{⍺ ⍵}⌸i

 :For (n ix) :InEach (ns ixs)
     :Select ⊃n
     :Case 'Event'
         values[ix]←normaliseEvents values[ix]
     :CaseList 'Points' 'Radius' 'Size'
         :If type≡'font'
             (ix⊃values)←⌊0.5+0.85×ix⊃values ⍝ Adjust font size down
         :ElseIf 2=⍴⍴val←ix⊃values
             (ix⊃values)←↓val
         :Else
             (ix⊃values)←,,¨val
         :EndIf
     :CaseList 'BCol' 'FCol' 'FillCol'
         (ix⊃values)←,⊆ix⊃values
     :CaseList 'LStyle' 'FStyle' 'LWidth'
         ⍝ Do nothing?
     :CaseList 'Visible' 'Active' 'Interval'
         (ix⊃values)←⊃ix⊃values ⍝ Must be scalar
     :Case 'Picture'
         (ix⊃values)←2↑(,⊆ix⊃values),3 ⍝ Default to Center
         n←≢c←(⍕caller),'.'
         (ix 1⊃values)←{(n×c≡n↑⍵)↓⍵}(ix 1⊃values)
     :CaseList 'File' 'FileNames'
         (ix⊃values)←(' ' ⎕R '_')ix⊃values
     :EndSelect
 :EndFor
