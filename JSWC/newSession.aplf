 rc←newSession args;hdrs;event;obj;rc;msg;s;ns;code;CONN;conn;max;key;space
 rc←1 ⍝ Fail
 (rc obj event hdrs)←4↑args
 hdrs←WSS.ParseHeaders hdrs

 :Hold 'JSWCSESSIONS'
     SESSIONID←⊃(⍳1+≢s)~s←⊃SESSIONS
     max←MAXSESSIONS
     :If SESSIONID>max
         'E' Log 'Unable to create more than ',(⍕max),' session',(max≠1)/'s'
         →0
     :EndIf
     :If MODE=2 ⍝ Multi
         code←⍎((⍕CODELOCATION),'_',⍕SESSIONID)⎕NS CODELOCATION ⍝ Clone the application namespace
         make_JSWC code
     :Else
         code←CODELOCATION      ⍝ If single user, just make a ref
     :EndIf
     (1⊃SESSIONS),←SESSIONID
     (2⊃SESSIONS),←conn←⊃2⊃⎕VFI ¯8↑obj
     SESSIONS,←space←⎕NS ''
     space.code←code
     rc←0
 :EndHold

 :If 0=⎕NC 'code._JSWC'
     'E' Log 'Handler: _JSWC not found in ',(⍕code)
     →0
 :EndIf
 ns←code._JSWC
 CONN←ns.ID←SESSIONID
 ns.addr←WSS.LDRC.GetProp obj'PeerAddr'
 ns.cert←WSS.LDRC.GetProp obj'PeerCert' ⍝ /// We'll use this later
 ns.conn←conn

 key←hdrs WSS.GetHeader'sec-websocket-key'
 'C' Log 'Session ',(⍕ns.ID),' created, socket ',(⍕ns.conn),', connected from ',(2 2⊃ns.addr), ', key: ',key

 :If 3=⎕NC'code.Init'
 :AndIf 0≠≢msg←code.Init ns
     'W' Log 'Unable to Init session from ',ns.addr,': ',msg
 :EndIf
