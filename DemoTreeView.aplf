 DemoTreeView connected;inputs;scores;folder;imagefiles;periods;z;⎕TRAP;m;players;val
 JSWC.CONNECTED←connected
 ⎕PW←1000

 n←10
 f←'+'
 allf←'+-×÷'

 imagefiles←'/images/'∘,¨'Folder-Closed.png' 'Folder-Open.png' 'Price-Ticket.png'
 'IL'∆WC'ImageList'('Size'(16 16))('Masked' 2)('Files' imagefiles)

 'F1'∆WC'Form' 'Function Table'(50 50)(800 800)('Coord' 'Pixel')
 'F1.MB'∆WC'MenuBar'
 'F1.MB.File'∆WC'Menu' '&File'
 'F1.MB.Col'∆WC'Menu' '&Colours'
 'F1.MB.Col.Red'∆WC'MenuItem' '&Red'('Event' 'Select' 'JSWCUpdate')
 'F1.MB.Col.Blue'∆WC'MenuItem' '&Blue'('Event' 'Select' 'JSWCUpdate')
 'F1.MB.Col.Green'∆WC'MenuItem' '&Green'('Event' 'Select' 'JSWCUpdate')
 'F1.MB.Focus'∆WC'Menu' '&Focus'
 'F1.MB.Focus.TableSize'∆WC'MenuItem' '&Size'('Event' 'Select' 'JSWCUpdate')
 'F1.MB.Focus.Function'∆WC'MenuItem' '&Function'('Event' 'Select' 'JSWCUpdate')
 'F1.MB.Focus.CalcBtn'∆WC'MenuItem' '&Calc Button'('Event' 'Select' 'JSWCUpdate')
 'F1.MB.Focus.Players'∆WC'MenuItem' '&Player Table'('Event' 'Select' 'JSWCUpdate')

 'F1.Players'∆WC'Grid'('ColTitles'('Name' 'Gender' 'Score' 'Expert'))('Posn'(10 10))('Size'(100 480))('TitleWidth' 0)('ShowInput' 1)('Event' 'CellChanged' 'JSWCUpdate')
 'F1.Players.Name'∆WC'Edit'('Size'(23 100))
 'F1.Players.Gender'∆WC'Combo'('Items'('Male' 'Female'))('Size'(10 100))
 'F1.Players.Score'∆WC'Edit'('FieldType' 'Numeric')('Size'(23 50))
 'F1.Players.Expert'∆WC'Button'('Style' 'Check')('Size'(23 25))
 inputs←'F1.Players.'∘,¨'Name' 'Gender' 'Score' 'Expert'
 scores←12 13
 players←'Amir' 'Fatima','Male' 'Female',scores,⍪0 1
 'F1.Players'∆WS('Values' players)('CellTypes'(2 4⍴1 2 3 4))('Input'inputs)
 'F1.Average'∆WC'Label'('Average Score:',1⍕+/scores÷≢scores)(115 180)(23 100)
 periods←'Q1' 'Jan' 'Feb' 'Mar' 'Q2' 'Apr' 'May' 'Jun'
 'F1.Months'∆WC'TreeView' periods (10 500)(150 150)('Depth'(8⍴0 1 1 1))('ImageListObj' '#.IL')('ImageIndex' (8⍴1 3 3 3))('Event' ('Expanding' 'Retracting') 'JSWCUpdate')

 'F1.TableSize'∆WC'Edit' '10'(210 10)(23 100)('FieldType' 'Numeric')('Event' 'Change' 'JSWCUpdate')
 'F1.Function'∆WC'Combo'(,¨allf)f(210 120)(23 100)('SelItems'(allf=f))('Event' 'Select' 'JSWCUpdate')
 'F1.CalcBtn'∆WC'Button' 'Calc'(210 230)(23 100)('Event' 'Select' 'JSWCUpdate')
 'F1.Table'∆WC'Grid'(∘.×⍨⍳n)(240 10)(550 780)

 'F1.PasswordField'∆WC'Edit' 'Password'(210 340)(23 100)('Password' '*')('Event' 'Change' 'JSWCUpdate')
 ∆NQ 'F1.TableSize' 'GotFocus'

 ⍝ Test ∆WG
  z←'F1.Months' ∆WG 'ImageIndex' ⍝ Static only
  :If z≢8⍴1 3 3 3
      ⎕←'*** Unexpected return value for F1.Months ImageIndex'
      ⎕←z
  :EndIf

  z←'F1.Function' ∆WG f←'Event' 'SelItems' 'Text' ⍝ Mix of Dynamic and Static
  :If ∨/m←z≢¨(,⊂'Select' 'JSWCUpdate') (1 0 0 0)(,'+')
      ⎕←'*** Unexpected return value for F1.Function: ',⍕m/f
      ⎕←z
  :EndIf

  z←'F1.TableSize' ∆WG f←'Text' 'Value' ⍝ Both Dynamic
  :If ∨/m←z≢¨'10' 10
      ⎕←'*** Unexpected return value for F1.TableSize: ',⍕m/f
      ⎕←z
  :EndIf

  z←'F1.Players' ∆WG 'Values' ⍝ Dynamic
  :If z≢players
      ⎕←'*** Unexpected return value for F1.Players.Values'
      ⎕←z
  :EndIf

  ⍝ Test Edit
  'F1.TableSize' ∆WS 'Text' '11'
  val←'F1.TableSize' ∆WG 'Value'
  :If val≢11
     ⎕←'*** After setting Text to "11", TableSize.Value is ',⍕val
  :EndIf

  'F1.TableSize' ∆WS 'Value' 9
  val←'F1.TableSize' ∆WG 'Text'
  :If val≢,'9'
     ⎕←'*** After setting Value to 9, TableSize.Text is ',⍕val
  :EndIf

  'F1.Function' ∆WS 'SelItems' (m←allf=f←'×')
  val←'F1.Function' ∆WG 'Text'
  :If val≢,f
     ⎕←'*** After setting SelItems to ',(⍕m),' Function.Text is ',val
  :EndIf
