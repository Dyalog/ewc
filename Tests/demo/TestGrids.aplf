r←TestGrids nul;rows;cols;f_status;cell;ir;ic;xp;o_cell;exp;t;xcell;src;ip;html;val;sel;ID;btn_Where;ID_c;chkbx
 (rows cols)←3 6
 r←''

 :If 0≡f_status←#.S.Find'F1.Average'
     r,←⊂'Cannot find F1.Average'
     →end    ⍝ not much we can sensibly test w/o the ability to control the feedback!
 :EndIf

 :If 'Total value'Check 11↑f_status.Text
     r,←⊂'F1.Average does not contain expected text "Total value..." but "',f_status.Text,'"'
 :EndIf
 :If 0≡btn_Where←#.S.Find'F1.Where'
     r,←⊂'Cannot find F1.Where' ⋄ →end ⋄ :EndIf

 :For cell :In 'GridCells'##.RandomVal 2⍴rows×cols   ⍝ click through cells in random order
     (ir ic)←2 1+3 6⊤¯1+cell                         ⍝ compute coordinates in grid (row[1;] are headers, so we needed to increment the offset)
     ⍝ xp←'//*[@id="F1.Holdings"]/div[',(⍕ir),']//div[contains(@class, "cell")][',(⍕ic),']'
    ⍝ o_cell←'XPath'#.S.Find xp
     ID←'F1.Holdings.r',(⍕ir),'.c',⍕ic
     ID_c←'F1.Holdings.CHECK.r',(⍕ir),'.c',⍕ic  ⍝ id of checkbox (if any)
     o_cell←#.S.Find ID
     :If 326 Check ⎕DR o_cell  ⍝ did we find cell?
         r,←⊂'Could not find cell with id="',ID,'"' ⋄ :Continue ⋄ :EndIf

     val←o_cell.Text

     :If ic=2⍝ inspect the cell to see if we have a checkbox in it
         o_cell.Click
         'DoubleClick'#.S.MoveToElement o_cell  ⍝ double click to activate input
         src←o_cell.GetAttribute⊂'innerHTML'    ⍝ the html of that cell
         :If 1 Check∨/'type="checkbox"'⍷src
             r,←⊂'Cell[',(⍕ir),';',(⍕ic),'] did not contain a checkbox but "',src,'"'
             :Continue  ⍝ we can't relly check this cell if it does not have a checkbox!
         :EndIf
         :If (⊂val)∊'Yes' 'No'
            ⍝ check if the checkbox reflects that...
             chkbx←#.S.Find ID_c   ⍝ find the checkbox
             :If (sel←chkbx.Selected)Check ¯1+'No' 'Yes'⍳⊂val
                 r,←⊂'Cell contained text "',val,'" but checkbox.Selected reported ',⍕ip.Selected
             :EndIf
             o_cell.Click   ⍝ change current  value
             (#.S.Find'F1.Table.r1.c1').Click       ⍝ click elsewhere (leave the cell)
             'DoubleClick'#.S.MoveToElement o_cell  ⍝ double click to activate input
             chkbx←#.S.Find ID_c                    ⍝ find checkbox again (it has been re-created!)
             :If 1 Check sel≠chkbx.Selected         ⍝ has the state changed?
                 r,←⊂'Value of cell[',(⍕ir),';',(⍕ic),'] did not change when clicking on it'
             :EndIf
             chkbx.Click  ⍝ reset to initial value
         :Else
             r,←⊂'Value in cell[',(⍕ir),';',(⍕ic),'] was not ∊ Yes/No but "',val,'"'
         :End
     :EndIf

     o_cell.Click
     btn_Where.Click

     exp←'Current cell is ',(⍕ir-1),' ',⍕ic   ⍝ expected value in status field
     :If ''Check t←f_status #.S.WaitFor exp
         r,←⊂'Moving to ',(⍕ir),'/',(⍕ic),' did not give status "',exp,'" but "',f_status.Text,'" (#130)'
     :EndIf

     :If ic∊3 4 6 ⍝ test if values in the columns are right-aligned
         :If 0=≢⊃('text-align:\s*right'⎕S'&')o_cell.GetAttribute⊂'innerHTML'
             r,←⊂'Cell[',(⍕ir),';',(⍕ic),'] does not seem to have style attributes to be right-aligned.' ⋄ :EndIf
     :EndIf
     ⍝ test editing!?
 :EndFor

⍝ also: click row/col in F1.Table and check that the correct cells are highlighted (css: background: rgb(199, 233, 255) - WIBNI there was a class for that?)

 :For ir :In 1+⍳10
     :For ic :In 1+⍳10
         ID←'F1.Table.r',(⍕ir),'.c',⍕ic
         o_cell←#.S.Find ID
         src←o_cell.GetAttribute⊂'outerHTML'    ⍝ the html of that cell
         :If 'right'Check⊃('text-align:\s*(left|right)'⎕S'\1')src
             r,←⊂'Cell [',(⍕ir),';',(⍕ic),'] does not seem to be right-aligned' ⋄ :EndIf
     :EndFor
 :EndFor

end:
 :If 0<≢r
     r←¯1↓∊(⊆r),¨⎕UCS 10
 :EndIf