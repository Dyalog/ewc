 r←TestPassword sink;xp;n;b;id;ok;p;v∆;val;out;kp;k;u;ev
 r←''

 ok←#.S.Find id←'F1.∆pP1.I999'
 :If 326 Check ⎕DR ok
     r,←⊂'Could not find "OK" Button (looked for id="',id,'")'
 :Else
     ok.Click  ⍝ this click should create an error log because the callback fn does not exist
 :EndIf

 p←#.S.Find'F1.∆pP1.B31001'
 out←#.S.Find'F1.∆pP1.H31004-t1'   ⍝ the output field

 :If 'password'Check v∆←p.GetAttribute⊂'type'    ⍝ testing the field for the password attribute seems to be the most we can do with Selenium...
     r,←⊂'Password field does not have type = "password", but "',v∆,'"'
 :EndIf
 p.Click

 :For val :In 'abCD' '0.123456789A' '⍳≢,2⌿3 4⍴0'    ⍝...apart from checking that values arrive correctly
     p #.S.SetInputValue val
     :If val Check v∆←p.GetAttribute⊂'value'
         r,←⊂'Password field did not contain input value "',val,'" but "',v∆,'"'
     :EndIf
 :EndFor

 p #.S.SetInputValue''

 :For k :In 'HelloTest42'
     p #.S.SendKeys k
     ⎕DL 1  ⍝ be sure callback is processed (there must be a better way to coordinate this...)
     kp←','(≠⊆⊢)ev←out.Text
     :If 'KeyPress'Check 2⊃kp
         r,←⊂'Sending key "',k,'" did not create a KeyPress event' ⋄ :EndIf
     :If k Check 3⊃kp
         r,←⊂'Sending key "',k,'" did not create a KeyPress event with that key in the argument passed to the callback fn' ⋄ :EndIf
     :If 6>≢kp
         r,←⊂'KeyPress event did not have 6 elements (but ',(⍕≢kp),'). Event args = ',ev
     :Else
         u←k∊⎕A  ⍝ is it an uppercase character?
         :If u Check 6⊃kp
             r,←⊂'Incorrect shift state when sending key "',k,'". Should be ',(⍕u),', but was reported as ',⍕6⊃kp ⋄ :EndIf
     :EndIf
 :EndFor

⍝ now leave the input
 #.S.Click'F1.∆pP1.I99'
 ⎕DL 1  ⍝ be sure callback is processed (there must be a better way to coordinate this...)
 kp←','(≠⊆⊢)out.Text
 :If 'F1.∆pP1.B31001'Check 1⊃kp
     r,←⊂'Clicking "OK" after entering data did not create a Change event for the input control (control="',(1⊃kp),'"' ⋄ :EndIf
 :If 'Change'Check 2⊃kp
     r,←⊂'Clicking "OK" after entering data did not create a Change event for the input control (event="',(2⊃kp),'"' ⋄ :EndIf
end:
